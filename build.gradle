plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id "com.github.jruby-gradle.base" version "1.6.0"
    id "java"
    id "checkstyle"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.4.2"
}

repositories {
    mavenCentral()
}

version = "0.25.0"
group = "com.github.takumakanari"
description = "Fetch data via http(s)"

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.35"
    compileOnly "org.embulk:embulk-spi:0.10.35"

    compile("org.embulk:embulk-util-config:0.3.1") {
        // They conflict with embulk-core. They are once excluded here,
        // and added explicitly with versions exactly the same with embulk-core:0.10.19.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }

    // They are once excluded from transitive dependencies of other dependencies,
    // and added explicitly with versions exactly the same with embulk-core:0.10.19.
    compile "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-core:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"
    compile "javax.validation:validation-api:1.1.0.Final"

    compile "org.embulk:embulk-util-timestamp:0.2.1"
    compile('org.embulk:embulk-util-json:0.1.1') {
        exclude group: "org.msgpack", module: "msgpack-core"
    }

    compile "org.embulk:embulk-util-retryhelper:0.8.1"
    compile "org.embulk:embulk-util-file:0.1.1"

    compile  "org.apache.httpcomponents:httpclient:4.5.9"
    compile  "commons-io:commons-io:2.6"

    testCompile "org.embulk:embulk-junit4:0.10.31"
    testCompile "org.embulk:embulk-core:0.10.31"
    testCompile "org.embulk:embulk-deps:0.10.31"
}

embulkPlugin {
    mainClass = "org.embulk.input.http.HttpFileInputPlugin"
    category = "input"
    type = "http"
}

task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
    repositories {
      // TODO: Configure some public Maven repo(s).
    }
}

gem {
  authors = ["Takuma kanari"]
  email = ["chemtrails.t@mail.com"]
  summary = "Embulk plugin for http input"
  homepage = "https://github.com/takumakanari/embulk-input-http"
  licenses = [ "MIT" ]
}

gemPush {
    host = "https://rubygems.org"
}


checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}

checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}

checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
